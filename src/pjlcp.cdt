!++
!   MODULE DESCRIPTION:
!
!       Routines for outputing help messages for PJL.
!
!   AUTHOR:         Tim E. Sneddon
!
!   Copyright (c) 2014, Endless Software Solutions.
!
!   All rights reserved.
!
!   This program is free software: you can redistribute it and/or modify
!   it under the terms of the GNU General Public License as published by
!   the Free Software Foundation, either version 3 of the License.
!
!   This program is distributed in the hope that it will be useful,
!   but WITHOUT ANY WARRANTY; without even the implied warranty of
!   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!   GNU General Public License for more details.
!
!   You should have received a copy of the GNU General Public License
!   along with this program.  If not, see <http://www.gnu.org/licenses/>.
!
!   CREATION DATE:  01-MAY-2014
!
!   MODIFICATION HISTORY:
!
!       01-MAY-2014 V1.0    Sneddon   Initial coding.
!       08-MAY-2014 V1.1    Sneddon   Defined QUOTE as "".  This will work
!                                     the same as a single in the parser
!                                     defintiion, but it will also stop cpp
!                                     from whinging about unterminated strings.
!       20-MAY-2014 V1.2    Sneddon   Modify SHOW CONNECTION. Add SET DUMP.
!       21-MAY-2014 V1.3    Sneddon   Add FSDELETE, FSMKDIR, FSUPLOAD, FSQUERY,
!                                     OPMSG, RDYMSG, STMSG.
!       21-MAY-2014 V1.3    Sneddon   Add OUTPUT to FSUPLOAD. Add FSDOWNLOAD.
!       29-SEP-2014 V1.4    Sneddon   Add FSPWD, FSCHDIR, FSENVIRONMENT.
!--
#include "keywords.h"
#include "operators.h"
#define QUOTE ""

ELEMENT START -
PROMPT "pjlcp> "
    CHAR e SUCCESS
    CHAR ! SUCCESS
    CHAR # SUCCESS
    CHAR ? GOTO help 
    WORD COMMENT MIN 3 CALL act_comment:OP_INIT GOTO comment
    WORD CONNECT MIN 3 CALL act_connect:OP_INIT GOTO connect
    WORD DISCONNECT MIN 3 CALL act_disconnect GOTO eol
    WORD ECHO MIN 3 CALL act_echo:OP_INIT GOTO echo
    WORD ENTER MIN 3 CALL act_enter:OP_INIT GOTO enter
    WORD EXIT MIN 3 CALL act_exit GOTO eol
    WORD FSCHDIR MIN 3 GOTO fschdir
    WORD FSDELETE MIN 4 CALL act_fsdelete:OP_INIT GOTO fsdelete
    WORD FSDIRLIST MIN 4 CALL act_fsdirlist:OP_INIT GOTO fsdirlist
    WORD FSDOWNLOAD MIN 4 CALL act_fsdown:OP_INIT GOTO fsdown
    WORD FSENVIRONMENT MIN 3 CALL act_fsenv:OP_INIT GOTO fsenv
    WORD FSMKDIR MIN 3 CALL act_fsmkdir:OP_INIT GOTO fsmkdir
    WORD FSPWD MIN 3 CALL act_fspwd GOTO eol
    WORD FSQUERY MIN 3 CALL act_fsquery:OP_INIT GOTO fsquery
    WORD FSUPLOAD MIN 3 CALL act_fsupload:OP_INIT GOTO fsupload
    WORD HELP MIN 3 CALL act_help:OP_INIT GOTO help
    WORD INFO MIN 3 CALL act_info:OP_INIT GOTO info
    WORD INQUIRE MIN 3 GOTO inquire
    WORD OPMSG MIN 3 CALL act_display:OP_INIT:KW_OPMSG GOTO display
    WORD PJL MIN 3 CALL act_pjl:OP_FINISH GOTO eol
    WORD PLEASE CALL act_please:OP_INIT:0 GOTO please
    WORD PLS CALL act_please:OP_INIT:1 GOTO please
    WORD PLZ CALL act_please:OP_INIT:2 GOTO please
    WORD QUIT MIN 3 CALL act_exit GOTO eol
    WORD RDYMSG MIN 3 CALL act_display:OP_INIT:KW_RDYMSG GOTO display
    WORD SET MIN 3 GOTO set
    WORD SHOW MIN 2 GOTO show
    WORD STMSG MIN 3 CALL act_display:OP_INIT:KW_STMSG GOTO display
    FORCE NEXT
ELEMENT *
    STRING ad CALL act_ivverb ERROR

!=============================================================================
! COMMENT [ remarks... ]
!
ELEMENT comment
    STRING adsp TERM/KEEP e CALL act_comment:OP_STORE:KW_COMMENT NEXT
    FORCE NEXT
ELEMENT *
    FORCE CALL act_comment:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! CONNECT NAME [ = ] hostname [ PORT [ = ] port ]
!
ELEMENT connect
    WORD NAME MIN 2 CALL act_connect:OP_CHECK:KW_NAME GOTO connect_name
    WORD PORT MIN 2 CALL act_connect:OP_CHECK:KW_PORT GOTO connect_port
    FORCE CALL act_connect:OP_FINISH GOTO eol

ELEMENT connect_name
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_connect:OP_STORE:KW_NAME GOTO connect

ELEMENT connect_port
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL MIN 1 CALL act_connect:OP_STORE:KW_PORT:0 GOTO connect

!    GOSUB parse_string CALL act_connect:OP_STORE:KW_PORT:1 GOTO connect
!
!=============================================================================


!=============================================================================
! ECHO [ words... ]
!
ELEMENT echo
    STRING adsp TERM/KEEP e CALL act_echo:OP_STORE:KW_ECHO NEXT
    FORCE NEXT
ELEMENT *
    FORCE CALL act_echo:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! ENTER LANGUAGE [ = ] personality
!
ELEMENT enter
    WORD LANGUAGE MIN 3 NEXT
    STRING ad CALL act_ivqual ERROR
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    STRING ad MIN 1 CALL act_enter:OP_STORE:KW_LANGUAGE GOTO eol

!
!=============================================================================


!=============================================================================
! FSCHDIR NAME [ = ] "pathname"
!
ELEMENT fschdir
    WORD NAME MIN 2 NEXT
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fschdir:OP_STORE:KW_NAME NEXT
ELEMENT *
    FORCE CALL act_fschdir:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! FSDELETE NAME [ = ] "pathname"
!
ELEMENT fsdelete
    WORD NAME MIN 2 NEXT
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsdelete:OP_STORE:KW_NAME NEXT
ELEMENT *
    FORCE CALL act_fsdelete:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! FSDIRLIST NAME [ = ] "..." [ ENTRY [ = ] n ] [ COUNT [ = ]n ]
!
ELEMENT fsdirlist
    WORD NAME MIN 2 CALL act_fsdirlist:OP_CHECK:KW_NAME GOTO fsdirlist_name
    WORD ENTRY MIN 2 CALL act_fsdirlist:OP_CHECK:KW_ENTRY GOTO fsdirlist_entry
    WORD COUNT MIN 2 CALL act_fsdirlist:OP_CHECK:KW_COUNT GOTO fsdirlist_count
    FORCE CALL act_fsdirlist:OP_FINISH GOTO eol

ELEMENT fsdirlist_name
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsdirlist:OP_STORE:KW_NAME GOTO fsdirlist

ELEMENT fsdirlist_entry
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL MIN 1 CALL act_fsdirlist:OP_STORE:KW_ENTRY GOTO fsdirlist

ELEMENT fsdirlist_count
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL MIN 1 CALL act_fsdirlist:OP_STORE:KW_COUNT GOTO fsdirlist
!
!=============================================================================


!=============================================================================
! FSDOWNLOAD [ FORMAT:BINARY ] NAME [ = ] "pathname" [ SIZE [ = ] n ]
!            [ INPUT [ = ] "local-pathname"
!
ELEMENT fsdown
    WORD FORMAT NEXT
    FORCE GOTO fsdown_attr
ELEMENT *
    CHAR : NEXT
ELEMENT *
    WORD BINARY NEXT
ELEMENT fsdown_attr
    WORD NAME MIN 2 CALL act_fsdown:OP_CHECK:KW_NAME GOTO fsdown_name
    WORD SIZE MIN 2 CALL act_fsdown:OP_CHECK:KW_SIZE GOTO fsdown_size
    WORD INPUT MIN 3 CALL act_fsdown:OP_CHECK:KW_OUTPUT GOTO fsdown_input
    FORCE CALL act_fsdown:OP_FINISH GOTO eol

ELEMENT fsdown_name
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsdown:OP_STORE:KW_NAME GOTO fsdown_attr

ELEMENT fsdown_size
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL MIN 1 CALL act_fsdown:OP_STORE:KW_SIZE GOTO fsdown_attr

ELEMENT fsdown_input
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsdown:OP_STORE:KW_INPUT GOTO fsdown_attr
!
!=============================================================================


!=============================================================================
! FSENVIRONMENT [ COMPACT [ = ] ON | OFF ] [ ITEMS [ = ] ON | OFF ]
!               [ LENGTH [ = ] ON | OFF ]
!
ELEMENT fsenv
    WORD COMPACT MIN 3 CALL act_fsenv:OP_CHECK:KW_COMPACT NEXT
    WORD ITEMS MIN 3 CALL act_fsenv:OP_CHECK:KW_ITEMS NEXT
    WORD LENGTH MIN 3 CALL act_fsenv:OP_CHECK:KW_LENGTH NEXT
    FORCE CALL act_fsenv:OP_FINISH GOTO eol
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    WORD OFF CALL act_fsenv:OP_STORE:KW_OFF GOTO fsenv
    WORD ON CALL act_fsenv:OP_STORE:KW_ON GOTO fsenv
!
!=============================================================================


!=============================================================================
! FSMKDIR NAME [ = ] "pathname"
!
ELEMENT fsmkdir
    WORD NAME MIN 2 NEXT
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsmkdir:OP_STORE:KW_NAME NEXT
ELEMENT *
    FORCE CALL act_fsmkdir:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! FSQUERY NAME [ = ] "pathname"
!
ELEMENT fsquery
    WORD NAME MIN 2 NEXT
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsquery:OP_STORE:KW_NAME NEXT
ELEMENT *
    FORCE CALL act_fsquery:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! FSUPLOAD NAME [ = ] "pathname" [ OFFSET [ = ] n ] [ SIZE [ = ] n ]
!           [ OUTPUT [ = ] "local-pathname"
!
ELEMENT fsupload
    WORD NAME MIN 2 CALL act_fsupload:OP_CHECK:KW_NAME GOTO fsupload_name
    WORD OFFSET MIN 2 CALL act_fsupload:OP_CHECK:KW_OFFSET GOTO fsupload_offset
    WORD SIZE MIN 2 CALL act_fsupload:OP_CHECK:KW_SIZE GOTO fsupload_size
    WORD OUTPUT MIN 3 CALL act_fsupload:OP_CHECK:KW_OUTPUT GOTO fsupload_output
    FORCE CALL act_fsupload:OP_FINISH GOTO eol

ELEMENT fsupload_name
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsupload:OP_STORE:KW_NAME GOTO fsupload

ELEMENT fsupload_offset
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL MIN 1 CALL act_fsupload:OP_STORE:KW_OFFSET GOTO fsupload

ELEMENT fsupload_size
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL MIN 1 CALL act_fsupload:OP_STORE:KW_SIZE GOTO fsupload

ELEMENT fsupload_output
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    GOSUB parse_string CALL act_fsupload:OP_STORE:KW_OUTPUT GOTO fsupload
!
!=============================================================================


!=============================================================================
! HELP [ <command> ]
!
ELEMENT help
    CHAR = GOTO help_value
    STRING ad CALL act_help:OP_STORE GOTO help
    FORCE CALL act_help:OP_FINISH GOTO eol

ELEMENT help_value
    DECIMAL MIN 1 GOTO help
    GOSUB parse_string GOTO help
    FORCE GOTO help
!
!=============================================================================


!=============================================================================
! INFO [ CONFIG | FILESYS | ID | INFO | MEMORY | PAGECOUNT | PRODINFO |
!        STATUS | USTATUS | VARIABLES ]
!
ELEMENT info
    CHAR s GOTO info
    CHAR e GOTO info_end
    WORD CONFIG MIN 3 CALL act_info:OP_STORE:KW_CONFIG GOTO info_end
    WORD FILESYS MIN 3 CALL act_info:OP_STORE:KW_FILESYS GOTO info_end
    WORD ID MIN 2 CALL act_info:OP_STORE:KW_ID GOTO info_end
    WORD INFO MIN 3 CALL act_info:OP_STORE:KW_INFO GOTO info_end
    WORD MEMORY MIN 3 CALL act_info:OP_STORE:KW_MEMORY GOTO info_end
    WORD PAGECOUNT MIN 3 CALL act_info:OP_STORE:KW_PAGECOUNT GOTO info_end
    WORD PRODINFO MIN 3 CALL act_info:OP_STORE:KW_PRODINFO GOTO info_end
    WORD STATUS MIN 3 CALL act_info:OP_STORE:KW_STATUS GOTO info_end
    WORD USTATUS MIN 3 CALL act_info:OP_STORE:KW_USTATUS GOTO info_end
    WORD VARIABLES MIN 3 CALL act_info:OP_STORE:KW_VARIABLES GOTO info_end
    FORCE NEXT
ELEMENT *
    STRING ad CALL act_ivverb ERROR

ELEMENT info_end
    FORCE CALL act_info:OP_FINISH GOTO eol
!
!=============================================================================


!=============================================================================
! INQUIRE [ LPARM [ : ] personality | IPARM  [ : ] port ] variable
!
ELEMENT inquire
    WORD LPARM MIN 3 GOTO inquire_lparm
    WORD IPARM MIN 3 GOTO inquire_iparm
    FORCE GOTO inquire_var

ELEMENT inquire_lparm
    CHAR : NEXT
    FORCE NEXT
ELEMENT *
    STRING ad MIN 1 GOTO inquire_var

ELEMENT inquire_iparm
    CHAR : NEXT
    FORCE NEXT
ELEMENT *
    STRING ad MIN 1 GOTO inquire_var

ELEMENT inquire_var
    STRING ad MIN 1 NEXT
ELEMENT *
    FORCE GOTO eol
!
!=============================================================================


!=============================================================================
! PLEASE HIDE 

ELEMENT please
    WORD HIDE CALL act_please:OP_FINISH:0 GOTO eol
    FORCE NEXT
ELEMENT *
    FORCE GOTO START
!
!=============================================================================


!=============================================================================
! OPMSG | RDYMSG | STMSG [ DISPLAY [ = ] ] "message"
!
ELEMENT display
    WORD DISPLAY MIN 3 NEXT
    FORCE GOTO message
ELEMENT *
    CHAR = NEXT
    FORCE NEXT
ELEMENT message
    GOSUB parse_string CALL act_display:OP_STORE:KW_DISPLAY NEXT
ELEMENT *
    FORCE CALL act_display:OP_FINISH GOTO eol
    
!
!=============================================================================


!=============================================================================
! SET [NO]AUTO_UEL | [NO]SUMMARY | [NO]TIMEOUT [ = ] n
!
ELEMENT set -
PROMPT "What? "
    WORD AUTO_UEL MIN 3 CALL act_set:OP_STORE:0 GOTO eol
    WORD NOAUTO_UEL MIN 4 CALL act_set:OP_STORE:1 GOTO eol
    WORD NOSUMMARY MIN 4 CALL act_set:OP_STORE:3 GOTO eol
    WORD SUMMARY MIN 3 CALL act_set:OP_STORE:2 GOTO eol
    WORD TIMEOUT MIN 3 GOTO set_timeout
    WORD NOTIMEOUT MIN 4 CALL act_set:OP_STORE:5 GOTO eol
    WORD DUMP MIN 3 CALL act_set:OP_STORE:6 GOTO eol
    WORD NODUMP MIN 4 CALL act_set:OP_STORE:7 GOTO eol
    FORCE NEXT
ELEMENT *
    STRING ad CALL act_ivverb ERROR

ELEMENT set_timeout
    CHAR = NEXT
    FORCE NEXT
ELEMENT *
    DECIMAL CALL act_set:OP_STORE:4 GOTO eol
!
!=============================================================================


!=============================================================================
! SHOW CONNECTION | VERSION
!
ELEMENT show -
PROMPT "What? "
    WORD CONNECTION MIN 3 CALL act_show:OP_FETCH:0 GOTO eol
    WORD VERSION MIN 3 CALL act_show:OP_FETCH:1 GOTO eol
    FORCE NEXT
ELEMENT *
    STRING ad CALL act_ivverb ERROR
!
!=============================================================================


!=============================================================================
! Eat up all whitespace until the end of the line.
!
ELEMENT eol
    STRING s GOTO eol
    CHAR e SUCCESS
    FORCE CALL act_eol ERROR

! Parse a quoted string.
!
ELEMENT parse_string
    CHAR QUOTE NEXT
    STRING adp SUCCESS
ELEMENT parse_string0
    STRING asdp TERM/SKIP QUOTE NEXT
    CHAR QUOTE NEXT
    FORCE CALL act_utstr ERROR
ELEMENT *
    CHAR QUOTE GOTO parse_string0
    FORCE SUCCESS

!ELEMENT parse_string
!    STRING asdp LEAD/INCLUDE QUOTE TERM/KEEP QUOTE NEXT
!    STRING adp SUCCESS
!    FORCE ERROR
!ELEMENT *
!    STRING QUOTE TERM/KEEP QUOTE GOTO parse_string
!    FORCE SUCCESS
!
!=============================================================================
